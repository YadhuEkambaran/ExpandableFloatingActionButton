plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.compose.compiler)

    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.yadhu.exfab'
    compileSdk 34

    defaultConfig {
        minSdk 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.compose.ui
    implementation libs.compose.ui.tooling
    implementation libs.material
    implementation libs.material.icons
    implementation libs.preview
}

publishing {
    publications {
        release(MavenPublication) {
            from components.findByName('release')

            groupId = 'io.github.yadhuekambaran'
            artifactId = 'expandablefab'
            version = '1.0.0'

            pom {
                name = 'ExpandableFloatingActionButton'
                description = 'This is a library for providing an expandable FloatingActionButton to Jetpack compose'
                url = 'https://github.com/YadhuEkambaran/ExpandableFloatingActionButton'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'YadhuEkambaran'
                        name = 'Yadhukrishnan Ekambaran'
                        email = 'yadhuekambaram1992@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/YadhuEkambaran/ExpandableFloatingActionButton.git'
                    developerConnection = 'scm:git:ssh://github.com/YadhuEkambaran/ExpandableFloatingActionButton.git'
                    url = 'https://github.com/YadhuEkambaran/ExpandableFloatingActionButton'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            findProperty('signing.keyId'),
            findProperty('signing.secretKey'),
            findProperty('signing.password')
    )
    sign publishing.publications.release
}